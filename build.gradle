plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.beryx.jlink' version '3.1.1'
}


repositories {
    mavenCentral()
}

dependencies {
    // JavaFX
    implementation 'org.openjfx:javafx-controls:21'
    implementation 'org.openjfx:javafx-fxml:21'

    // Gson
    implementation 'com.google.code.gson:gson:2.13.1'

    // Gstreamer
    implementation 'org.freedesktop.gstreamer:gst1-java-core:1.4.0'

    // Iconli
    implementation 'org.kordamp.ikonli:ikonli-core:12.4.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.4.0'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome6-pack:12.4.0'

    // JDBC
    implementation 'org.xerial:sqlite-jdbc:3.44.1.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-simple:1.7.32'


}

javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.fxml']
}

application {
    mainModule = 'com.anilabs.anilabsfx'
    mainClass = 'com.anilabs.anilabsfx.Main'
}


jlink {
    addExtraDependencies("javafx")

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'AniLabsFX'
        jvmArgs = [
                '-Djava.library.path=$APPDIR\\gstreamer\\win\\bin'
        ]
    }

    jpackage {
        installerType = 'exe'
        installerOptions = [
                '--win-menu',                          // Добавить в меню "Пуск"
                '--win-shortcut',                      // Создать ярлык на рабочем столе
                '--win-dir-chooser',                   // Позволить выбрать папку установки
                '--win-per-user-install',              // Установка для текущего пользователя
                '--vendor', 'AniLabs',                 // Имя разработчика
                '--app-version', '0.0.1'               // Версия приложения
        ]

        resourceDir = file('gstreamer/win')

        imageName = 'AniLabsFX'
        installerName = 'AniLabsFX-Installer'
    }
}

